//*****************************************************************************
// Copyright (c) 2017-2026 - Allen Cummings, RealmsMUD, All rights reserved. See
//                      the accompanying LICENSE file for details.
//*****************************************************************************
virtual inherit "/lib/core/thing.c";
#include "/lib/modules/secure/relationships.h"

/////////////////////////////////////////////////////////////////////////////
private nomask object relationshipService()
{
    return getService("relationship");
}

/////////////////////////////////////////////////////////////////////////////
private nomask string realizationKey(mixed realization)
{
    string ret = "";

    if (objectp(realization))
    {
        string actorName = realization->Name() ? realization->Name() : "any";
        ret = sprintf("%s#%s", program_name(realization), actorName);
    }
    else if (stringp(realization))
    {
        ret = realization;
    }

    return ret;
}

/////////////////////////////////////////////////////////////////////////////
private nomask mapping cloneRelationship(mapping relationship)
{
    mapping ret = relationship + ([]);

    if (member(ret, "dimensions") && mappingp(ret["dimensions"]))
    {
        ret["dimensions"] = ret["dimensions"] + ([]);
    }

    return ret;
}

/////////////////////////////////////////////////////////////////////////////
private nomask mapping cloneHistory(mapping entry)
{
    mapping ret = entry + ([]);

    if (member(ret, "context") && mappingp(ret["context"]))
    {
        ret["context"] = ret["context"] + ([]);
    }
    if (member(ret, "metadata") && mappingp(ret["metadata"]))
    {
        ret["metadata"] = ret["metadata"] + ([]);
    }

    return ret;
}

/////////////////////////////////////////////////////////////////////////////
private nomask int historyMatches(mapping entry, mapping query)
{
    int ret = 1;

    if (mappingp(query) && sizeof(query))
    {
        foreach(string key in m_indices(query))
        {
            if (!member(entry, key) || (entry[key] != query[key]))
            {
                ret = 0;
                break;
            }
        }
    }

    return ret;
}

/////////////////////////////////////////////////////////////////////////////
private nomask mapping *mergeHistory(mapping *existingHistory,
    mapping *newHistory)
{
    mapping *ret = pointerp(existingHistory) ? existingHistory + ({ }) : ({ });

    if (pointerp(newHistory))
    {
        foreach(mapping entry in newHistory)
        {
            int duplicate = 0;

            foreach(mapping existing in ret)
            {
                duplicate =
                    (existing["dimension"] == entry["dimension"]) &&
                    (to_int(existing["delta"]) == to_int(entry["delta"])) &&
                    (to_int(existing["value"]) == to_int(entry["value"])) &&
                    (to_int(existing["timestamp"]) == to_int(entry["timestamp"]));

                if (duplicate)
                {
                    break;
                }
            }

            if (!duplicate)
            {
                ret += ({ cloneHistory(entry) });
            }
        }
    }

    return ret;
}

/////////////////////////////////////////////////////////////////////////////
public nomask mapping updateRelationshipToward(mixed target,
    mapping dimensionChanges, mapping context, mapping metadata, mixed producer)
{
    mapping ret = ([]);
    object service = relationshipService();

    if (service)
    {
        string targetKey = realizationKey(target);

        if (member(relationships, targetKey) &&
            !sizeof(service->relationshipToward(this_object(), targetKey)))
        {
            service->seedRelationship(this_object(), targetKey,
                relationships[targetKey]["dimensions"],
                relationships[targetKey]["updated"]);
        }

        ret = service->updateRelationship(this_object(), target,
            dimensionChanges, context, metadata, producer);

        if (sizeof(ret))
        {
            relationships[targetKey] = cloneRelationship(ret);

            mapping *existing = member(relationshipHistory, targetKey) ?
                relationshipHistory[targetKey] : ({ });
            mapping *fromService =
                service->relationshipHistoryFor(this_object(), target, ([]));

            relationshipHistory[targetKey] = mergeHistory(existing, fromService);
        }
    }

    return ret;
}

/////////////////////////////////////////////////////////////////////////////
public nomask mapping relationshipToward(mixed target)
{
    mapping ret = ([]);
    string targetKey = realizationKey(target);

    if (member(relationships, targetKey))
    {
        ret = cloneRelationship(relationships[targetKey]);
    }
    return ret;
}

/////////////////////////////////////////////////////////////////////////////
public nomask int relationshipDimensionToward(mixed target, string dimension)
{
    int ret = 0;
    mapping relationship = relationshipToward(target);

    if (mappingp(relationship) && member(relationship, "dimensions") &&
        member(relationship["dimensions"], dimension))
    {
        ret = to_int(relationship["dimensions"][dimension]);
    }

    return ret;
}

/////////////////////////////////////////////////////////////////////////////
public nomask int hasRelationshipToward(mixed target)
{
    return sizeof(relationshipToward(target));
}

/////////////////////////////////////////////////////////////////////////////
public nomask mapping *queryRelationships(mapping query)
{
    mapping *ret = ({ });

    foreach(string target in m_indices(relationships))
    {
        mapping relationship = relationships[target];
        int matches = 1;

        if (mappingp(query) && sizeof(query))
        {
            foreach(string key in m_indices(query))
            {
                if (key == "target")
                {
                    matches = (relationship["target"] == realizationKey(query[key]));
                }
                else if (member(relationship["dimensions"], key))
                {
                    matches = (to_int(relationship["dimensions"][key]) >=
                        to_int(query[key]));
                }
                else
                {
                    matches = 0;
                }

                if (!matches)
                {
                    break;
                }
            }
        }

        if (matches)
        {
            ret += ({ cloneRelationship(relationship) });
        }
    }

    return ret;
}

/////////////////////////////////////////////////////////////////////////////
public nomask mapping *relationshipHistoryToward(mixed target, mapping query)
{
    mapping *ret = ({ });
    string targetKey = realizationKey(target);

    if (member(relationshipHistory, targetKey) &&
        pointerp(relationshipHistory[targetKey]))
    {
        foreach(mapping entry in relationshipHistory[targetKey])
        {
            if (historyMatches(entry, query))
            {
                ret += ({ cloneHistory(entry) });
            }
        }
    }

    return ret;
}
