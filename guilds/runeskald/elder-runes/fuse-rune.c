//*****************************************************************************
// Copyright (c) 2017-2026 - Allen Cummings, RealmsMUD, All rights reserved. See
//                      the accompanying LICENSE file for details.
//*****************************************************************************
inherit "/lib/modules/research/activeResearchItem.c";

/////////////////////////////////////////////////////////////////////////////
protected void Setup()
{
    addSpecification("name", "Fuse Rune");
    addSpecification("source", "runeskald");
    addSpecification("description", "This ritual allows the runeskald to "
        "permanently fuse a rune-stone from their inventory into a piece "
        "of equipped weapon or armor, granting its bonuses to the bearer.");

    addPrerequisite("level",
        (["type": "level", "guild": "runeskald", "value": 1]));
    addPrerequisite("/guilds/runeskald/elder-runes/root.c",
        (["type": "research"]));

    addSpecification("scope", "self");
    addSpecification("research type", "points");
    addSpecification("research cost", 1);
    addSpecification("command template", "fuse rune ##Target##");
    addSpecification("use ability message",
        "##InitiatorName## traces ancient symbols in the air, binding a rune into ##TargetName##.");
    addSpecification("stamina point cost", 20);
    addSpecification("spell point cost", 15);
}

/////////////////////////////////////////////////////////////////////////////
protected nomask int executeOnSelf(string unparsedCommand, object owner,
    string researchName)
{
    string runePattern = "^fuse rune (.+) into (.+)$";
    string *parts = regexp(({ unparsedCommand }), runePattern);

    if (!sizeof(parts))
    {
        notify_fail("Usage: fuse rune <rune name> into <item name>\n");
        return 0;
    }

    string runeName = regreplace(unparsedCommand,
        "^fuse rune (.+) into (.+)$", "\\1");
    string itemName = regreplace(unparsedCommand,
        "^fuse rune (.+) into (.+)$", "\\2");

    object rune = present(runeName, owner);
    if (!rune || !function_exists("isRune", rune) || !rune->isRune())
    {
        notify_fail(sprintf("You do not have a %s.\n", runeName));
        return 0;
    }

    object item = present(itemName, owner);
    if (!item || !function_exists("fuseRune", item))
    {
        notify_fail(sprintf("You do not have a %s that can have runes fused into it.\n",
            itemName));
        return 0;
    }

    return item->fuseRune(rune);
}
