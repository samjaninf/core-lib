// *****************************************************************************
// Copyright (c) 2017-2026 - Allen Cummings, RealmsMUD, All rights reserved.
//                      See the accompanying LICENSE file for details.
// *****************************************************************************

/////////////////////////////////////////////////////////////////////////////
public nomask string escapeString(string value)
{
    value = regreplace(value, "\\\\", "\\\\\\\\", 1);
    value = regreplace(value, "\"", "\\\\\"", 1);
    value = regreplace(value, "\n", "\\\\n", 1);
    value = regreplace(value, "\r", "\\\\r", 1);
    value = regreplace(value, "\t", "\\\\t", 1);
    return value;
}

/////////////////////////////////////////////////////////////////////////////
public nomask string encodeValue(mixed value)
{
    string ret = "null";

    if (stringp(value))
    {
        ret = "\"" + escapeString(value) + "\"";
    }
    else if (intp(value) || floatp(value))
    {
        ret = to_string(value);
    }
    else if (pointerp(value))
    {
        string *elements = ({ });
        foreach (mixed element in value)
        {
            elements += ({ encodeValue(element) });
        }
        ret = "[" + implode(elements, ",") + "]";
    }
    else if (mappingp(value))
    {
        string *pairs = ({ });
        foreach (string key in sort_array(m_indices(value), (: $1 > $2 :)))
        {
            pairs += ({ "\"" + escapeString(key) + "\":" + encodeValue(value[key]) });
        }
        ret = "{" + implode(pairs, ",") + "}";
    }

    return ret;
}
